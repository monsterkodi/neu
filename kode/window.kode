###
000   000  000  000   000  0000000     0000000   000   000
000 0 000  000  0000  000  000   000  000   000  000 0 000
000000000  000  000 0 000  000   000  000   000  000000000
000   000  000  000  0000  000   000  000   000  000   000
00     00  000  000   000  0000000     0000000   00     00
###

import post from './post.js'
import Title from './title.js'

function Window

    @: ->
        
        Neutralino.events.on 'clientConnect'    @onClientConnect
        Neutralino.events.on 'appClientConnect' @onAppClientConnect
        Neutralino.events.on 'windowFocus'      @onWindowFocus
        Neutralino.events.on 'windowBlur'       @onWindowBlur
        
        post.on 'menuAction' @onMenuAction
        
        window.titlebar = new Title icon:'./icons/app.png'
        
        document.addEventListener 'DOMContentLoaded' @onDomLoaded
            
    onDomLoaded: => 
        
        dragging = false
        posX = 0
        posY = 0
        
        draggable = document.getElementById 'titlebar'
          
        draggable.onmousedown = (e) ->
    
            posX = e.pageX
            posY = e.pageY
            dragging = true
            null
          
        window.onmouseup = (e) ->
            dragging = false
            null
          
        document.onmousemove = (e) -> 
            if dragging
                Neutralino.window.move e.screenX - posX, e.screenY - posY
            null
        
    toggleMaximize: =>
        
        Neutralino.window.isMaximized().then (isMaximized) ->
            if isMaximized
                Neutralino.window.unmaximize()
            else
                Neutralino.window.maximize()
        
    onWindowFocus: (event) => log 'onFocus' event.detail
    onWindowBlur:  (event) => log 'onBlur'  event.detail
    
    onAppClientConnect: (event) => log 'onAppClientConnect' event.detail
    onClientConnect:    (event) => log 'onClientConnect'    event.detail
                
    onMenuAction: (action) =>
        
        switch action.toLowerCase()
             
            'maximize'  ➜ @toggleMaximize()
            'minimize'  ➜ Neutralino.window.minimize()
            # 'close'     ➜ Neutralino.app.exit()
            'close'     ➜ Neutralino.window.hide()

export Window
