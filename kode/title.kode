###
000000000  000  000000000  000      00000000
   000     000     000     000      000     
   000     000     000     000      0000000 
   000     000     000     000      000     
   000     000     000     0000000  00000000
###

import dom      from './dom.js'    # use dom
import sds      from './sds.js'
import menu     from './menu.js'   
import noon     from './noon.js'  
import keyinfo  from './keyinfo.js'  
import slash    from './slash.js'  
import post     from './post.js'   # use dom elem slash post 
                                
{ $, stopEvent, elem } = dom    # use $ stopEvent from dom

function Title
    
    @: (@opt) ->
        
        @opt ?= {}
        
        pkg = @opt.pkg
        
        @elem =$ @opt.elem ? "#titlebar"
        
        @elem.classList.add 'focus'
        
        return if not @elem
        
        post.on 'pointerMove' @onPointerMove
        post.on 'titlebar'    @onTitlebar
        post.on 'menuAction'  @onMenuAction
        post.on 'windowFocus' @onWindowFocus
        post.on 'windowBlur'  @onWindowBlur
        
        @elem.addEventListener 'dblclick' (event) -> 
            stopEvent event 
            post.emit 'menuAction' 'Maximize'
                
        @winicon = elem class: 'winicon'
        
        if @opt.icon
            imgSrc = slash.join @opt.dir, @opt.icon
            @winicon.appendChild elem 'img' src:imgSrc
        
        @elem.appendChild @winicon
        @winicon.addEventListener 'click' -> post.emit 'menuAction' 'Open Menu'   
        
        @title = elem class:'titlebar-title app-drag-region' id:'title'
        @elem.appendChild @title
        
        @setTitle @opt
                
        # â€” â—» ðŸž©
        
        @minimize = elem class: 'winbutton minimize gray'
        
        @minimize.innerHTML = """
            <svg width="100%" height="100%" viewBox="-10 -8 30 30">
                <line x1="-1" y1="5" x2="11" y2="5"></line>
            </svg>
        """
        
        @elem.appendChild @minimize
        @minimize.addEventListener 'click' -> post.emit 'menuAction' 'Minimize'
        
        @maximize = elem class: 'winbutton maximize gray'
        
        @maximize.innerHTML = """
            <svg width="100%" height="100%" viewBox="-10 -9 30 30">
              <rect width="11" height="11" style="fill-opacity: 0;"></rect>
            </svg>
        """
        @elem.appendChild @maximize
        @maximize.addEventListener 'click' -> post.emit 'menuAction' 'Maximize'

        @close = elem class: 'winbutton close'
        
        @close.innerHTML = """
            <svg width="100%" height="100%" viewBox="-10 -9 30 30">
                <line x1="0" y1="0" x2="10" y2="11"></line>
                <line x1="10" y1="0" x2="0" y2="11"></line>
            </svg>
        """
        
        @elem.appendChild @close
        @close.addEventListener 'click' -> post.emit 'menuAction' 'Close'

        @topframe = elem class: 'topframe'
        @elem.appendChild @topframe
        
        @initMenu()
       
    pushElem: (elem) ->
        
        @elem.insertBefore elem, @minimize
            
    showTitle: -> @title.style.display = 'initial'
    hideTitle: -> @title.style.display = 'none'
    
    onPointerMove: (x, y) => #Neutralino.debug.log "mouse #{x} #{y}"
    
    onWindowFocus: => 
        @elem.classList.remove 'blur'
        @elem.classList.add 'focus'

    onWindowBlur: => 
        @elem.classList.remove 'focus'
        @elem.classList.add 'blur'
        
    #  0000000  00000000  000000000  000000000  000  000000000  000      00000000  
    # 000       000          000        000     000     000     000      000       
    # 0000000   0000000      000        000     000     000     000      0000000   
    #      000  000          000        000     000     000     000      000       
    # 0000000   00000000     000        000     000     000     0000000  00000000  
    
    setTitle: (opt) ->
        
        html = ""
        
        parts = opt.title ? []
        
        if opt.pkg
            if opt.pkg.name and 'name' in parts
                html += "<span class='titlebar-name'>#{opt.pkg.name}</span>"
            
            if opt.pkg.version and 'version' in parts
                html += "<span class='titlebar-dot'>#{opt.pkg.version}</span>"
                
            if opt.pkg.path and 'path' in parts
                html += "<span class='titlebar-dot'> â–º </span>"
                html += "<span class='titlebar-name'>#{opt.pkg.path}</span>"
            
        @title.innerHTML = html
                    
    onTitlebar: (action) =>
        
        switch action
            'showTitle'   âžœ @showTitle()
            'hideTitle'   âžœ @hideTitle()
            'showMenu'    âžœ @showMenu()
            'hideMenu'    âžœ @hideMenu()
            'toggleMenu'  âžœ @toggleMenu()
            
    # 00     00  00000000  000   000  000   000   0000000    0000000  000000000  000   0000000   000   000  
    # 000   000  000       0000  000  000   000  000   000  000          000     000  000   000  0000  000  
    # 000000000  0000000   000 0 000  000   000  000000000  000          000     000  000   000  000 0 000  
    # 000 0 000  000       000  0000  000   000  000   000  000          000     000  000   000  000  0000  
    # 000   000  00000000  000   000   0000000   000   000   0000000     000     000   0000000   000   000  
    
    onMenuAction: (action) =>
                
        switch action.toLowerCase()
             
            'toggle menu' âžœ @toggleMenu()
            'open menu'   âžœ @openMenu()
            'show menu'   âžœ @showMenu()
            'hide menu'   âžœ @hideMenu()

    # 000  000   000  000  000000000  00     00  00000000  000   000  000   000  
    # 000  0000  000  000     000     000   000  000       0000  000  000   000  
    # 000  000 0 000  000     000     000000000  0000000   000 0 000  000   000  
    # 000  000  0000  000     000     000 0 000  000       000  0000  000   000  
    # 000  000   000  000     000     000   000  00000000  000   000   0000000   
    
    initMenu: ->
                    
        return [] if not @opt.menu
                
        if empty @templateCache
            noon.fetch @opt.menu, (tc) => 
                if valid tc
                    @templateCache = @makeTemplate tc
                    if @opt.menuTemplate? and @opt.menuTemplate is func
                        @templateCache = @opt.menuTemplate @templateCache
                    @initFromCache()
                else
                    error 'title.initMenu - empty template?' @opt.menu
        else
            @initFromCache()
            
    initFromCache: ->
        
        @menu = new menu items:@templateCache
        @elem.insertBefore @menu.elem, @elem.firstChild.nextSibling
        # @hideMenu()
                
    makeTemplate: (obj) ->
        
        tmpl = []
        for text,menuOrAccel of obj
            tmpl.push if
                empty menuOrAccel and text.startsWith '-'
                    text: ''
                menuOrAccel is num
                    text:text
                    accel:kstr menuOrAccel
                menuOrAccel is str
                    text:text
                    accel:keyinfo.convertCmdCtrl menuOrAccel
                empty menuOrAccel
                    text:text
                    accel: ''
                menuOrAccel.accel? or menuOrAccel.command? # needs better test!
                    item = clone menuOrAccel
                    item.text = text
                    item
                else
                    text:text
                    menu:@makeTemplate menuOrAccel
        tmpl
        
    refreshMenu: ->
        
        @menu.del()
        @initMenu()

    menuVisible: => @menu.elem.style.display != 'none'
    showMenu:    => @menu.elem.style.display = 'inline-block'; @menu?.focus?()
    hideMenu:    => @menu?.close(); @menu.elem.style.display = 'none'
    toggleMenu:  => if @menuVisible() then @hideMenu() else @showMenu()
    openMenu:    => if @menuVisible() then @hideMenu() else @showMenu(); @menu.open()

    # 000   000  00000000  000   000
    # 000  000   000        000 000
    # 0000000    0000000     00000
    # 000  000   000          000
    # 000   000  00000000     000

    handleKeyInfo: (modKeyComboEvent) ->

        { mod, key, combo, event } = modKeyComboEvent
        
        # log "mod #{mod} key #{key} combo #{combo}"
        
        if empty combo
            # log 'no combo'
            return 'unhandled'
        
        menu = @templateCache
        
        if empty menu
            log 'no menu'
            return 'unhandled'
        
        accels = sds.find.key menu, 'accel'
        combos = sds.find.key menu, 'combo'
        
        for keypath in combos.concat accels # swap on win?
            combos = sds.get(menu, keypath).split ' '
            combos = combos.map (c) -> keyinfo.convertCmdCtrl c
            if combo in combos
                keypath.pop()
                item = sds.get menu, keypath
                action = item.action ? item.text
                log 'menuAction' action
                post.emit 'menuAction' action
                return action

        'unhandled'
            
export Title
